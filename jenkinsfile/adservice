def dockerImage

pipeline {
    agent any

    tools {
        jdk 'jdk21'
    }

    environment {
        // Docker
        DOCKER_REPO = 'rakesh6nm'
        IMAGE_NAME  = 'adservice'
        IMAGE_FULL  = "${DOCKER_REPO}/${IMAGE_NAME}"
        IMAGE_TAG   = "${BUILD_NUMBER}"

        // GitOps
        GITOPS_REPO = 'git@github.com:rakesh611/complete-prodcution-pipeline.git'
        GITOPS_DIR  = 'gitops'
    }

    stages {

        stage('Checkout SCM') {
            steps {
                checkout scm
            }
        }

        stage('Trivy FS Scan') {
            steps {
                dir('src/adservice') {
                    sh '''
                      trivy fs . \
                        --severity CRITICAL,HIGH \
                        --scanners vuln,misconfig,secret \
                        --format table \
                        --output trivy-fs-report.txt
                    '''
                }
            }
            post {
                always {
                    archiveArtifacts artifacts: 'src/adservice/trivy-fs-report.txt', fingerprint: true
                }
            }
        }

        stage('Build Docker Image') {
            steps {
                dir('src/adservice') {
                    script {
                        dockerImage = docker.build("${IMAGE_FULL}:${IMAGE_TAG}")
                        dockerImage.tag("latest")
                    }
                }
            }
        }

        stage('Push Docker Image') {
            steps {
                script {
                    docker.withRegistry('https://index.docker.io/v1/', 'dockerhub1') {
                        dockerImage.push("${IMAGE_TAG}")
                        dockerImage.push("latest")
                    }
                }
            }
        }

        stage('Update GitOps Repo') {
            steps {
                sshagent(['gitops_ssh']) {
                    sh """
                        rm -rf Google-microservices-devsecops-project
                        git clone ${GITOPS_REPO} Google-microservices-devsecops-project

                        cd Google-microservices-devsecops-project/k8s/adservice

                        echo "Updating deployment image to ${IMAGE_FULL}:${IMAGE_TAG}"
                        sed -i "s|image: ${IMAGE_NAME}:.*|image: ${IMAGE_FULL}:${IMAGE_TAG}|" deployment.yaml

                        git config user.email "jharakesh485@gmail.com"
                        git config user.name "Rakesh Jha"

                        git add deployment.yaml
                        git commit -m "Deploy adservice ${IMAGE_TAG}" || echo "No changes to commit"

                        git push origin main
                    """
                }
            }
        }
    }

    post {
        always {
            script {
                if (fileExists('src/adservice/trivy-fs-report.txt')) {
                    emailext(
                        subject: "Pipeline ${currentBuild.currentResult}: ${env.JOB_NAME} #${env.BUILD_NUMBER}",
                        mimeType: 'text/html',
                        to: 'jharakesh485@gmail.com',
                        body: """
                        <h3>Adservice DevSecOps Pipeline</h3>
                        <p><b>Status:</b> ${currentBuild.currentResult}</p>
                        <p><b>Job:</b> ${env.JOB_NAME}</p>
                        <p><b>Build:</b> ${env.BUILD_NUMBER}</p>
                        <p><b>URL:</b> <a href="${env.BUILD_URL}">${env.BUILD_URL}</a></p>
                        """,
                        attachmentsPattern: 'src/adservice/trivy-fs-report.txt'
                    )
                }
            }
        }
    }
}
